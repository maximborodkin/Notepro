plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "ru.modemastudio.notepro"
        minSdk 21
        targetSdk 31
        versionCode 3
        versionName "1.2"
        multiDexEnabled true
    }

    signingConfigs {
        release {
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))

            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable = false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
}

dependencies {
    def dagger_version = "2.40.4"
    def room_version = "2.4.1"
    def nav_version = "2.3.5"
    def livedata_version = "2.4.0"
    def coroutines_version = "1.5.2"
    def markwon_version = '4.6.2'

    // KTX
    implementation 'androidx.core:core-ktx:1.7.0'

    // DI
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    // Images
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    // Scrolling indicator
    implementation 'ru.tinkoff.scrollingpagerindicator:scrollingpagerindicator:1.2.1'

    // Database
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // View binding
    implementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.5.0-beta01'

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Livecycle
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$livedata_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$livedata_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$livedata_version"

    // Markwon { @link https://github.com/noties/Markwon }
    implementation "io.noties.markwon:core:$markwon_version"
    implementation "io.noties.markwon:editor:$markwon_version"
    implementation "io.noties.markwon:ext-tables:$markwon_version"
    implementation "io.noties.markwon:ext-tasklist:$markwon_version"
    implementation "io.noties.markwon:ext-strikethrough:$markwon_version"
    implementation "io.noties.markwon:html:$markwon_version"
    implementation "io.noties.markwon:image:$markwon_version"
    implementation "io.noties.markwon:linkify:$markwon_version"
    implementation "com.caverock:androidsvg-aar:1.4"

    // Scrolling indicator
    implementation 'ru.tinkoff.scrollingpagerindicator:scrollingpagerindicator:1.2.1'

    // Preferences
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // Logging
    implementation 'com.jakewharton.timber:timber:5.0.1'
}